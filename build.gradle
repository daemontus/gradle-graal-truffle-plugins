plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'maven-publish'
}

group = 'org.graalvm.plugin'
version = '0.1.0-alpha01'

repositories { jcenter() }

java { sourceCompatibility JavaVersion.VERSION_1_8 }

dependencies { testImplementation 'junit:junit:4.13' }

pluginBundle {
    website = 'https://www.graalvm.org/'
    vcsUrl = 'https://github.com/daemontus/gradle-graal-truffle-plugins'
    tags = ['graal', 'compiler', 'native-image', 'truffle', 'language']
}

gradlePlugin {
    plugins {
        create("compiler") { PluginDeclaration plugin ->
            plugin.id = 'org.graalvm.plugin.compiler'
            plugin.displayName = 'Graal Compiler Plugin'
            plugin.description = 'Automatically enable Graal compiler on Hotspot JVMs and in distributions. Allow truffle languages as project dependencies.'
            plugin.implementationClass = 'com.oracle.truffle.gradle.CompilerPlugin'
        }
        create("native-image") { PluginDeclaration plugin ->
            plugin.id = 'org.graalvm.plugin.native-image'
            plugin.displayName = 'Graal Native Image Plugin'
            plugin.description = 'Declare NativeImage tasks for building native binaries of Java applications.'
            plugin.implementationClass = "com.oracle.truffle.gradle.NativeImagePlugin"
        }
        create("language") { PluginDeclaration plugin ->
            plugin.id = 'org.graalvm.plugin.truffle-language'
            plugin.displayName = 'Graal Language Plugin'
            plugin.description = 'Configure the project as Graal&Truffle language.'
            plugin.implementationClass = 'com.oracle.truffle.gradle.LanguagePlugin'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}
